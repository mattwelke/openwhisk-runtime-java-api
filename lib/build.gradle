plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.mattwelke.owr.java'
version = '1.0.0'

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'openwhisk-runtime-java-api'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = "Action API for OpenWhisk Java runtimes"
                description = 'Common library containing the API between runtimes and actions for the runtimes. Used to help OpenWhisk users implement actions, ex. through code completion tools in IDEs.'
                url = 'https://github.com/mattwelke/openwhisk-runtime-java-api'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mattwelke'
                        name = 'Matt Welke'
                        email = 'mattwelke@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/mattwelke/openwhisk-runtime-java-api.git'
                    developerConnection = 'git@github.com:mattwelke/openwhisk-runtime-java-api.git'
                    url = 'https://github.com/mattwelke/openwhisk-runtime-java-api'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mattwelke/openwhisk-runtime-java-api"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
